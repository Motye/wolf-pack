	.TITLE UBOAT2
	.RADIX 16
BEAR1	=0			;SHIP BEARIN 0-1023
SBEAR	=70			;SHIP BEARING SAVE
PICT1	=4			;SHIP PICTURE CODE
ACT1	=75			;SHIP MODE, -1=INACTIVE, 0=ACTIVE, 1=EXPLODING
INDEX1	=6			;SHIP LIFE INDEX
VIS1	=77			;SHIP VISIBILITY FLAG
HPOS1	=8			;SHIP HORIZONTAL POSITION
OFF1	=79			;SHIP OFFSET 0-63
SIZE1	=0A			;SHIP SIZE
SIDE1	=7B			;SHIP SIDE REFLECT
SPEED1	=0C			;SHIP SPEED 55-0FF
BEARL	=7D			;SHIP BEARING LSB
COINS	=0E			;COINS
CREDIT	=0F			;CREDIT
KOIN1	=10			;COIN 1 DEBOUNCE COUNTER
KOIN2	=11			;COIN 2 DEBOUNCE COUNTER
BEARP	=12			;PERISCOPE BEARING 0-1023
OBEAR	=14			;PERISCOPE BEARING SAVE
QOLD	=16			;PERISCOPE QUADRATURE SAVE
MODE	=17			;GAME MODE
DFLAG	=18			;DESTROYER DONE FLAG
DONE	=19			;GAME DONE FLAG
INVRT	=1A			;VIDEO INVERT FLAG
TBUTS	=1B			;TORPEDO BUTTON STATUS
TOG	=1C			;TOGGLE OUTPUT FLAG
POLYL	=1D			;RANDOM NUMBER SEED
POLYH	=1E			;RANDOM NUMBER SEED
CFLG	=1F			;SHIP COLLISION FLAG
TB1	=20			;TORPEDO 1 BEARING 0-1023
TB2	=22			;TORPEDO 2 BEARING 0-1023
VPS1	=24			;TORP 1 VERTICAL POSITION
VPS2	=25			;TORP 2 VERTICAL POSITION
AT1	=26			;TORP 1 MODE, -1=INACTIVE, 0=ACTIVE
AT2	=27			;TORP 2 MODE
PCT1	=28			;TORP 1 PICTURE CODE
PCT2	=29			;TORP 2 PICTURE CODE
VS1	=30			;TORP 1 VISIBILITY FLAG
VS2	=31			;TORP 2 VISIBILITY FLAG
PTACT	=32			;PT BOAT MODE, -1=INACTIVE,
				;0=ACTIVE BOAT, 1=ACTIVE SCORE,
				;2=EXPLODING BOAT
PTSEEN	=33			;PT BOAT SEEN FLAG
PTPCT	=34			;PT BOAT PICTURE CODE
PTVIS	=35			;PT BOAT VISIBILITY FLAG
PTBEAR	=36			;PT BOAT BEARING 0-1023
PTHPOS	=38			;PT BOAT HORIZONTAL POSITION
SSEL	=39			;PT BOAT SCREEN SIDE SELECT
PTEXP	=3A			;PT BOAT EXPLOSION COUNT
PTCNT	=3B			;PT BOAT LIFE COUNTER
PTSP	=3C			;PT BOAT SPEED LSB
PTLOW	=3D			;PTBOAT BEARING LSB
PTSPEED	=3E			;PTBOAT SPEED MSB
MTIM	=3E			;MESSAGE TIMER
VCOUNT	=84			;VISIBILITY COUNT
HSCR	=40			;HIGH SCORE
SCRL	=42			;SCORE LSB'S
SCRH	=43			;SCORE MSB'S
PTR	=44			;TEMPORARY POINTER
TEMP1	=46			;TEMPORARY LOCATIONS
TEMP2	=48
TEMP3	=4A
TEMP4	=4C
TUP	=4E			;GAME TIMER
TCNT	=4F			;GAME TIMER SAVE
TIME	=50			;GAME TIMER OUTPUT
SFLG	=51			;SIREN MODE, 0=0FF, +=# OF TIMES
STOG	=52			;SIREN TOGGLE
LEXP	=53			;LOW EXPLOSION COUNTER
HEXP	=54			;HIGH EXPLOSION COUNTER
TEXP	=55			;TORPEDO SOUND COUNTER
SIF	=56			;SONAR PING FLAG
SING	=57			;SONAR PING COUNTER
MIF	=58			;MOTOR FLAG
VOL	=59			;VOLUME CONTROL
HPS1	=5A			;TORP 1 HORIZONAL POSITION
HPS2	=5B			;TORP 2
CPCT	=5C			;CLOUD PICTURE CODE
CHPOS	=5D			;CLOUD HORIZONTAL POSITION
CVIS	=5E			;CLOUD VISIBILITY FLAG
CSEL	=5F			;CLOUD SCREEN SIDE SELECT
FRAME	=60			;FRAME COUNTER
MTIM	=61			;MESSAGE TIMER
RTEST	=62			;RAM TEST LOCATIONS
INPNT	=64			;CIRCULAR BUFFER IN POINTER FOR VOICE
OUTPNT	=65			;CIRCULAR BUFFER OUT POINTER FOR VOICE
VTOG	=66        		;BETWEEN WORD DISABLE VOICE TIMER
TCMT	=67
TEMP5	=80			;TEMPORARY LOCATION
TOGL	=82			;EVERY OTHER FRAME TOGGLE OUTPUT FLAG
WSHIP	=83			;WHICH FRAME COLLISION OCCURRED FLAG(PREVIOUS TOG)
DTIM	=86			;REACTIVATE DESTROYER TIME AMOUNT
EVAL	=87			;EXTENDED PLAY LEVEL
FFLG	=89			;GOOD COIN FLAGS
VOXTIM	=8B			;VOICE DISABLE TIMER
TOGK	=8C			;FIRE 1,FIRE 2 TOGGLE
VBUFF	=90
TEST	=9000
SB	=1000
CSW	=3000
TBUT	=1000
VAMT	=0C0
TESTV	=0AA
RAMMER	=0A
SIZEO	=4002
HPOSO	=4000
PCTO	=4001
HPSO	=4005
VPSO	=4006
PICTO	=4004
OFFO	=4003
SIREN	=2002
LEX	=2006
MCON	=2007
CLITE	=200C
HEX	=2000
PING	=2001
TEX	=2005
BOTH	=3000
VINT	=2008
REFLT	=2009
PTSND	=2003		;PT MOTOR SOUND CONTROL
PTPIK	=3003		;PT BOAT PICTURE CODE CONTROL
PTHOR	=3001		;PT BOAT HORIZONTAL CONTROL
PTSD	=3003		;PT BOAT SCREEN SIDE SELECT
ATTK	=200D
HSEL	=200E
ELITE	=200A
COLR	=3007
TREST	=5000
TSW	=3000
QUIN	=1000
AMODE	=2000
COINAD	=1000
TOMSD	=109D
TOLSD	=109E
POUT	=100B
BOUT	=1029
ALPHA	=1000
ELSD	=10B5
EHSD	=10B4
SBUSY	=2000
SPOUT	=3004
SPST	=2004
PROJ	=200F
	.SBTTL POWER ON LOGIC
	.ASECT
	.=7000
PWRON:	SEI			;POWER ON, LOCKOUT IRQ
	CLD			;CLEAR DECIMAL MODE
	LDX I,0FF		;SET UP STACK
	TXS
	LDA A,TEST		;PRODUCTION DIAGNOSTIC PRESENT?
	CMP I,TESTV
	BNE BEGIN		;NO, START PROGRAM
	JMP TEST		;YES, GO TO DIAGNOSTIC
BEGIN:	JSR SETUP		;DO PWRON INITIALIZATION
MAIN:	JSR ATRACT
	JSR START
	JMP MAIN
	.SBTTL ATTRACT ROUTINE
ATRACT:	JSR INITL
	LDA I,1
	STA A,ATTK
	STA A,CLITE		;TURN OFF START LITE
	LDX I,0
	LDA I,80		;ACTIVATE A SHIP
	STA ZX,SIZE1
	LDA I,0
	STA ZX,ACT1
	STA ZX,OFF1
	LDA I,1
	STA ZX,SIDE1
	STA ZX,VIS1
	LDA I,0FF
	STA ZX,HPOS1
	LDA I,0
	STA ZX,PICT1
	LDA A,CSW
	AND I,3
	CLC
	ADC I,2
	JSR MESS
1$:	LDX I,0
	LDA ZX,HPOS1
	BNE 2$
	INC ZX,OFF1
	LDA ZX,OFF1
	CMP I,40
	BCC 3$
	JMP ATRACT
2$:	DEC ZX,HPOS1
3$:	JSR WAIT
	JSR SOUND
	JSR VOICE
	LDA A,TSW		;PLAY ME OPTION ENABLED?
	AND I,20
	BEQ 4$
	INC TEMP4		;MAJOR TIME PASSED?
	BNE 5$			;NO
	INC TEMP4+1
	LDA TEMP4+1
	CMP I,96
	BCC 5$			;NO
	LDA I,0			;YES
	STA TEMP4+1
6$:	LDA I,1
	STA SFLG
	LDA I,-1
	STA STOG
	LDA I,4
	JSR STUFF
	LDA I,6
	JSR STUFF
	LDA I,4
	JSR STUFF
	LDA I,6
	JSR STUFF
	LDA BEARP
	AND I,2
	STA OBEAR
	JMP 4$
5$:	INC TCNT
	BNE 4$
	JSR FIRE			;LOOK FOR CONTROL CHANGE
	BEQ 6$
	LDA BEARP
	AND I,2
	CMP OBEAR
	BNE 6$
	STA OBEAR
	DEC TCNT
4$:	LDA CREDIT
	BEQ 1$
	DEC CREDIT
	RTS
	.SBTTL START ROUTINE
START:	LDA I,0E
	JSR STUFF
	LDA I,19
	JSR STUFF
3$:	LDA A,SB		;WAIT FOR START BUTTON
	AND I,20
	BNE S1
	LDA FRAME
	AND I,10
	BNE 1$
	LDA I,1
	STA A,CLITE
	LDA I,0A
	BNE 2$
1$:	LDA I,0
	STA A,CLITE
	LDA I,8A
2$:	JSR MESS
	JSR WAIT		;WAIT FOR VBLANK
	JSR SOUND
	JSR VOICE
	LDA FRAME
	BNE 3$
	BEQ START
S1:	LDA I,0
	STA SCRL
	STA SCRH
	STA VOXTIM
	STA A,CLITE
	STA A,ATTK
	JSR INITL
	LDA I,8A
	JSR MESS
	LDA I,8E
	JSR MESS
	LDA I,1			;TURN ON PING
	STA SIF
	LDA I,-1
	STA SING
S3:	JSR PLAY
	LDA VOXTIM
	BEQ 1$
	DEC VOXTIM
1$:	LDA DONE		;ALL DONE?
	BNE S2			;YES
	JSR WAIT		;NO, WAIT FOR VBLANK
	JMP S3
S2:	LDA I,1
	STA A,ATTK		;TURN ON ATTRACK
	LDA HSCR+1		;UPDATE HIGH SCORE
	CMP SCRH
	BEQ 1$
	BCS 2$
3$:	LDA SCRH
	STA HSCR+1
	LDA SCRL
	STA HSCR
	JMP 2$
1$:	LDA HSCR
	CMP SCRL
	BCC 3$
2$:	LDA I,0
	STA FRAME
	STA INVRT
	STA A,MCON
	STA SFLG
	STA A,SIREN
	LDA OUTPNT		;TURN OFF VOICE
	STA INPNT
7$:	LDA FRAME
	AND I,10
	BNE 4$
	LDA I,8			;OUTPUT YOU ARE SUNK
	BNE 5$
4$:	LDA I,88
5$:	JSR MESS
6$:	JSR WAIT
	LDA FRAME
	BNE 7$
	RTS
	.SBTTL PLAY SUBROUTINE
PLAY:	LDX I,0
PY1:	LDA ZX,ACT1		;SHIP ACTIVE?
	BPL P1
	CPX I,0
	BNE 2$
	LDA POLYL
	CMP I,32.
	BCS 2$
	LDA MODE
	BNE 2$
	LDA I,2
	STA SFLG
	LDA I,-1
	STA STOG
	JSR AVAT		;EVERY SO OFTEN ACTIVATE
	JMP P7			;DESTROYER
2$:	LDA POLYH		;NO, ACTIVATE IT
	AND I,1
	STA ZX,SIDE1
1$:	TXA
	ASL
	TAY
	LDA POLYL
	STA AY,BEAR1
	LDA POLYH
	AND I,3
	STA AY,BEAR1+1
	JSR FIELD1			;SEE IF SHIP WILL BE VISIBLE
	LDA ZX,VIS1
	BEQ P6				;NO, NO PROBLEM
	JSR RANDU			;YES, TRY AGAIN
	JMP 1$
P6:	LDA POLYL
	AND I,7
	TAY
	LDA AY,SNUM
	STA ZX,PICT1
	LDA I,0
	STA ZX,ACT1
	STA ZX,SIZE1
	STA ZX,BEARL
	LDA I,VAMT
	STA ZX,VCOUNT
1$:	LDA POLYL		;PICK A SPEED 55-0FF
	CMP I,55
	BCS 6$
	JSR RANDU
	JMP 1$
6$:	STA ZX,SPEED1
	LDA I,0
	STA ZX,INDEX1
	JSR RANDU
	JMP P7
P1:	LDA ZX,VIS1		;SHIP VISIBLE?
	BNE P2			;YES, NO PROBLEM
	INC ZX,VCOUNT		;BEEN INVISIBLE FOR AWHILE?
	BNE P5			;NO, NO PROBLEM
	LDA ZX,PICT1		;DISABLE LOGIC
	CMP I,RAMMER		;FOR DESTROYER
	BEQ P2
	TXA
	ASL
	TAY
	LDA BEARP		;YES, CHANGE BEARING
	SEC			;TO PROCEDE PERISCOPE AND
	SBC OBEAR		;COMING TOWARD PERISCOPE
	LDA BEARP+1
	SBC OBEAR+1
	BCC 5$
	BNE 2$
	LDA POLYL		;IF NO CHANGE, MAKE
	AND I,1			;SIDE RANDOM
	BEQ 2$
5$:	LDA BEARP
	SEC
	SBC I,40
	STA AY,BEAR1
	LDA BEARP+1
	SBC I,0
	STA AY,BEAR1+1
	LDA I,0
	STA ZX,SIDE1
	JMP P6
2$:	LDA BEARP+1
	CLC
	ADC I,1
	AND I,3
	STA AY,BEAR1+1
	LDA I,1
	STA ZX,SIDE1
	LDA BEARP
	STA AY,BEAR1
	JMP P6			;REACTIVATE SHIP
P2:	LDA I,VAMT		;RESET VISIBILITY COUNTER
	STA ZX,VCOUNT
P5:	LDA ZX,ACT1		;EXPLOSION OR POSITION UPDATE?
	BEQ P4
	BMI P3
	JSR EXPLD		;EXPLOSION UPDATE
	JMP P3
P4:	JSR UPDT1		;POSITION UPDATE
P3:	JSR FIELD1		;UPDATE FIELD OF VIEW
P7:	INX
	CPX I,2
	BCS 4$
	JMP PY1
4$:	JSR PTBOAT
	JSR CLOUDS
	JSR FIRE		;FIRE BUTTON PRESSED?
	BMI 5$			;NO
	LDX I,0			;YES, SEARCH FOR
6$:	LDA ZX,AT1		;AVAILABLE TORPEDO
	BPL 7$
	LDA I,0			;ROUND AVAILABLE TORP
	STA ZX,AT1		;ACTIVATE
	STA A,TEX
	LDA I,84
	STA TEXP
	LDA I,0C8
	STA ZX,VPS1
	TXA
	ASL
	TAY
	LDA BEARP
	CLC
	ADC I,127.
	STA AY,TB1
	LDA BEARP+1
	ADC I,0
	AND I,3
	STA AY,TB1+1
	LDA VOXTIM
	BNE 5$
	LDA I,0B
	JSR STUFF
	LDA I,40
	STA VOXTIM
	LDY I,11			;CHOOSE FIRE 1 OR FIRE 2
	LDA TOGK			;AND ALSO TOGGLE TOGK
	BEQ 10$
	INY
10$:	EOR I,1
	STA TOGK
	TYA
	JSR STUFF
	JMP 5$
7$:	INX
	CPX I,2
	BNE 6$
5$:	LDX I,0
9$:	LDA ZX,AT1
	BMI 8$
	JSR UPSD
	JSR COLL
8$:	INX
	CPX I,2
	BNE 9$
	LDA MTIM		;MESSAGE TIMER ACTIVE?
	BMI 1$			;NO
	DEC MTIM		;YES, ALL DONE?
	BNE 1$			;NO
	LDA I,-1		;YES, DEACTIVATE TIMER
	STA MTIM
	LDA I,89		;ERASE MESSAGE
	JSR MESS
	LDA PTACT		;IF SCORE ACTIVE
	CMP I,1			;DEACTIVATE IT
	BNE 1$
	LDA I,-1
	STA PTACT
	LDA I,0F
	STA PTPCT
1$:	LDA BEARP		;SAVE LAST PERISCOPE BEARING
	STA OBEAR
	LDA BEARP+1
	STA OBEAR+1
	LDA I,0
	JSR OSCR		;OUTPUT SCORE
	LDA MODE
	CMP I,1
	BEQ 12$
	CMP I,2
	BNE P61
12$:	LDA FRAME
	AND I,20
	BNE 2$
	LDA I,7
	BNE 3$
2$:	LDA I,87
3$:	JSR MESS
P61:	LDA TCNT		;TIME TO UPDATE TIMER?
	BEQ P40			;YES
	DEC TCNT		;NO, DECREMENT COUNTER
	BPL P42
P40:	LDA TUP			;TIMER UPDATE
	STA TCNT		;RESET TIMER
	LDA TIME		;DECREMENT SCREEN TIME
	BEQ P41			;MAKE SURE TIME NOT LESS THAN 0
	SED
	SEC
	SBC I,1
	STA TIME
	CLD
P41:	LDA TIME		;CONVERT BCD SCREEN TIME
	AND I,0F0		;TO ASCII SCREEN TIME
	LSR
	LSR
	LSR
	LSR
	ORA I,30
	STA A,TOMSD
	LDA TIME
	AND I,0F
	ORA I,30
	STA A,TOLSD
P42:	JSR SOUND		;DO SOUND
	JSR VOICE		;DO VOICE
	LDA MODE		;CHECK MODE
	BNE P43
	LDA TIME		;MODE=0, NORMAL PLAY
	BNE 1$			;WAIT FOR TIME=0
	LDA EVAL+1		;CHECK FOR EXTENDED
	CMP SCRH		;PLAY LEVEL
	BCC 2$
	BEQ 3$
	BCS P46
3$:	LDA SCRL
	CMP EVAL
	BCC P46			;NO EXTENDED PLAY
2$:	LDA I,1
	STA MODE		;GOT EXTENDED PLAY
	LDA I,20
	STA TIME
1$:	JMP P50
P46:	LDA I,5			;GET MODE=5
	STA MODE
	LDA I,0A0		;ACTIVATE EXPLOSION
	STA LEXP
	STA HEXP
	BNE P50
P43:	CMP I,1
	BNE P45
	LDA TIME		;MODE=1,TIME.LE.10?
	CMP I,10
	BCC 1$			;YES
	BEQ 2$			;YES
	JMP P50			;NO
2$:	LDA SFLG
	BNE 1$
	LDA I,9
	STA SFLG
	LDA I,-1
	STA STOG
1$:	LDA VIS1
	BNE P50
	JSR AVAT		;ACTIVATE DESTROYER
	LDA I,2			;SET MODE=2
	STA MODE
	BNE P50
P45:	CMP I,2
	BNE P47
	LDA TIME		;MODE=2,TIME=0?
	BEQ P46			;YES, ALL DONE
	LDA DFLAG		;NO, SHIP DESTROYED?
	BEQ P50			;NO
	LDA HEXP		;YES, LOW EXPLOSION FINISHED?
	BNE P50			;NO
	LDA DTIM		;YES, REACTIVATE NEXT
	STA TIME		;DESTROYER WITH SHORTER
	DEC DTIM		;TIME LEFT
	JSR AVAT
	JMP P50
P47:	CMP I,5
	BNE P50
	LDA INVRT
	EOR I,1
	STA INVRT
	LDA AT1			;IF EITHER TORP
	BEQ P50			;ACTIVE DO NOT
	LDA AT2			;END GAME
	BEQ P50
	LDA LEXP		;EXPLOSION SOUND STILL ACTIVE?
	BNE P50			;YES
	LDA I,1			;NO, SET DONE FLAG
	STA DONE
P50:	RTS
	.SBTTL PT BOAT LOGIC
PTBOAT:	LDA PTACT		;PT BOAT ALREADY ACTIVE?
	BPL 1$			;YES, IGNORE THIS LOGIC
	LDA FRAME		;TIME TO ACTIVATE BOAT?
	CMP POLYL
	BNE 1$			;NO
	LDA I,0			;YES, ACTIVATE BOAT
	STA PTACT
	STA PTSEEN
	STA PTLOW
	LDA POLYH		;SELECT PT SPEED
	AND I,1			;IN THE RANGE
	STA PTSPEED		;OF 080-1FF
	BNE 10$
12$:	LDA POLYL
	CMP I,80
	BCS 11$
	JSR RANDU
	JMP 12$
10$:	LDA POLYL
11$:	STA PTSP
	LDA PICT1		;IF RAMMER
	CMP I,RAMMER		;ACTIVE DO NOT USE MOTOR
	BEQ 17$
	LDA I,0
	STA A,PTSND		;SELECT PT SOUND
	LDA I,1
	STA MIF			;TURN ON PT SOUND
17$:	LDA POLYL		;SELECT LEFT OR RIGHT SIDE
	AND I,1
	BNE 2$
	LDA I,0			;LEFT SIDE, SELECT
	STA PTPCT		;CORRECT PICTURE
	LDA BEARP
	SEC
	SBC I,20
	STA PTBEAR
	LDA BEARP+1
	SBC I,0
	AND I,3
	STA PTBEAR+1
	BPL 1$
2$:	LDA I,1			;RIGHT SIDE
	STA PTPCT
	LDA BEARP
	CLC
	ADC I,20
	STA PTBEAR
	LDA BEARP+1
	ADC I,1
	AND I,3
	STA PTBEAR+1
1$:	LDA PTACT		;BOAT ACTIVE?
	BNE PT4			;NO
	LDA PTSEEN		;PT BOAT EVER BEEN SEEN?
	BEQ PT3			;NO, KEEP IT GOING
	LDA PTVIS		;PT BOAT STILL VISIBLE?
	BNE PT3			;YES, KEEP IT GOING
	DEC PTCNT		;NO, BEEN OFF SCREEN
	BNE PT3			;FOR QUARTER SECOND?
	LDA I,0F			;YES, DEACTIVATE BOAT
	STA PTPCT
	LDA I,-1
	STA PTACT
	LDA PICT1
	CMP I,RAMMER
	BEQ 20$
	LDA I,0
	STA MIF			;TURN OFF MOTOR
20$:	JMP PT1
PT3:	LDA PTPCT		;YES, UPDATE POSITION
	CMP I,1
	BNE PT5
	LDA PTLOW
	SEC
	SBC PTSP
	STA PTLOW
	LDA PTBEAR
	SBC PTSPEED
	STA PTBEAR
	LDA PTBEAR+1
	SBC I,0
	AND I,3
	STA PTBEAR+1
	BPL PT4
PT5:	LDA PTLOW
	CLC
	ADC PTSP
	STA PTLOW
	LDA PTBEAR
	ADC PTSPEED
	STA PTBEAR
	LDA PTBEAR+1
	ADC I,0
	AND I,3
	STA PTBEAR+1
PT4:	LDA I,0
	STA PTVIS
	LDA PTBEAR
	SEC
	SBC BEARP
	STA TEMP4
	LDA PTBEAR+1
	SBC BEARP+1
	AND I,3			;RESULT WITHIN 256?
	BNE 5$			;NO
	LDA TEMP4		;YES, SHIP VISIBLE
	STA PTHPOS
	CMP I,07F		;SELECT CORRECT SIDE OF SCREEN
	BCS 6$
	LDA I,3
	BPL 7$
6$:	LDA I,1
7$:	STA SSEL
	BNE 8$
5$:	LDA BEARP
	SEC
	SBC PTBEAR
	STA TEMP4
	LDA BEARP+1
	SBC PTBEAR+1
	AND I,3
	BNE 9$
	LDA TEMP4
	CMP I,20
	BCS 9$
	EOR I,0FF
	STA PTHPOS
	LDA I,2
	STA SSEL
8$:	INC PTVIS
	LDA I,1
	STA PTSEEN
	LDA I,30
	STA PTCNT
9$:	LDA PTACT		;PT BOAT EXPLODING?
	CMP I,2
	BNE PT2			;NO
	LDA PTEXP		;YES, DO A EXPLOSION UPDATE
	AND I,7			;SELECT CORRECT EXPLOSION PICTURE
	TAY
	LDA AY,PTEP
	STA PTPCT
	LDA FRAME
	AND I,2
	LSR
	STA A,ELITE
	DEC PTEXP
	BNE PT2
	LDA I,0F			;EXPLOSION ALL DONE
	STA PTPCT		;CLEAR PICTURE
	LDA I,-1		;GET PT INACTIVE
	STA PTACT
	LDA I,0
	STA A,ELITE
PT2:	LDA PTACT		;COLLISION LOGIC
	BNE PT1			;PT BOAT ACTIVE?
	LDX I,0			;YES
2$:	LDA ZX,AT1		;TORP ACTIVE?
	BNE 1$			;NO, TRY NEXT TORP
	TXA
	ASL
	TAY
	LDA AY,TB1		;CHECK BEARING POSITION
	SEC
	SBC PTBEAR
	STA TEMP4
	LDA AY,TB1+1
	SBC PTBEAR+1
	BNE 1$
	LDA TEMP4
	CMP I,9
	BCC 1$
	CMP I,24
	BCS 1$
	LDA ZX,VPS1		;CHECK VERTICAL POSITION
	CMP I,74
	BCS 1$
	CMP I,70
	BCC 1$
	LDA I,-1		;WE'VE GOT A HIT
	STA ZX,AT1		;DEACTIVATE TORPEDO
	LDA I,84		;YES, ACTIVATE EXPLOSION
	STA LEXP
	STA PTEXP		;GET UP EXPLOSION SEQUENCE COUNTER
	LDA I,2
	STA PTACT		;GET PT SHIP TO EXPLODING
	LDA I,9			;OUTPUT SCORE MESSAGE
	JSR MESS		;AND UPDATE SCORE
	LDA I,37
	STA A,POUT
	LDA SCRL
	SED
	CLC
	ADC I,7
	STA SCRL
	LDA SCRH
	ADC I,0
	STA SCRH
	CLD
	LDA I,70
	STA MTIM
	LDA PICT1		;TURN MOTOR OFF IF
	CMP I,RAMMER		;RAMMER NOT ACTIVE
	BEQ PT1
	LDA I,0
	STA MIF
	STA A,MCON
	JMP PT1
1$:	INX
	CPX I,2
	BNE 2$
PT1:	RTS
PTEP:	.BYTE 3,3,2,2
	.BYTE 7,7,7,7
	.SBTTL CLOUD LOGIC
CLOUDS:	LDX I,0
6$:	LDA AX,CBEAR
	SEC
	SBC BEARP
	STA TEMP4
	LDA AX,CBEAR+1
	SBC BEARP+1		;CLOUD VISIBLE?
	AND I,3			;RESULT WITHIN 256?
	BNE 1$			;NO
	LDA TEMP4		;YES, CLOUD VISIBLE
	STA CHPOS
	CMP I,07F
	BCS 2$
	LDA I,3
	BPL 3$
2$:	LDA I,1
3$:	STA CSEL
	BNE 4$
1$:	LDA BEARP		;SEE IF CLOUD PARTIALLY
	SEC			;VISIBLE ON LEFT SIDE
	SBC AX,CBEAR
	STA TEMP4
	LDA BEARP+1
	SBC AX,CBEAR+1
	AND I,3
	BNE 5$
	LDA TEMP4
	CMP I,20
	BCS 5$
	EOR I,0FF
	STA CHPOS
	LDA I,2
	STA CSEL
4$:	LDA I,1
	STA CVIS
	TXA
	LSR
	TAX
	LDA AX,CPICT
	STA CPCT
	RTS
5$:	INX
	INX
	CPX I,6
	BNE 6$
	LDA I,0
	STA CVIS
	RTS
CBEAR:	.WORD 07F,1D4,329
CPICT:	.BYTE 8,9,0A
	.SBTTL TORPEDO POSITION UPDATE
UPSD:	LDA ZX,VPS1		;ALL DONE?
	CMP I,50
	BEQ U10			;YES
	CMP I,0A0
	BCS 2$
	CMP I,80
	BCS 3$
	LDA FRAME
	AND I,1
	BEQ 4$
	BNE 2$
3$:	LDA FRAME
	AND I,3
	BEQ 4$
2$:	DEC ZX,VPS1		;DECREMENT VERTICAL POSITION
	DEC ZX,VPS1
	DEC ZX,VPS1
4$:	LDY I,0F
	LDA ZX,VPS1
	CMP I,70
	BCC 1$
	EOR I,0FF
	AND I,0F0
	LSR
	LSR
	LSR
	LSR
	SEC
	SBC I,2
	ASL
	TAY
1$:	STY ZX,PCT1
	TXA
	ASL
	TAY
	LDA AY,TB1		;TORPEDO IN FIELD-OF-VIEW?
	SEC
	SBC BEARP
	STA TEMP1
	LDA AY,TB1+1
	SBC BEARP+1
	AND I,3
	BNE U11			;NO
	LDA TEMP1		;YES, OUTPUT NEW HPOS
	CMP I,10
	BCC U11
	STA ZX,HPS1
	LDA I,1
	STA ZX,VS1		;TORP VISIBLE, SET FLAG
	RTS			;RETURN
U11:	LDA I,0
	STA ZX,VS1
	RTS
U10:	LDA I,-1
	STA ZX,AT1		;SET TORPEDO INACTIVE
	LDA I,0D0
	STA ZX,VPS1
	RTS
	.SBTTL COLLISION LOGIC
COLL:	STX TEMP3
	LDA ZX,VPS1
	SEC	
	SBC I,5
	CMP I,66		;VERTICAL POSITIONS
	BCC C2			;WITHIN LIMITS?
C4:	LDX TEMP3
	RTS
C2:	LDX WSHIP
	LDA CFLG		;COLLISION?
	BNE C42			;YES
	LDX I,0
C41:	STX TEMP5
	LDA ZX,ACT1		;SHIP ACTIVE?
	BNE C40			;NO
	LDA ZX,VIS1		;SHIP VISIBLE?
	BNE C40			;YES, IGNORE THIS LOGIC
	LDA TEMP3		;THIS IS SOFTWARE
	ASL			;COLLISION DETECT FOR OFF-
	TAY			;SCREEN SHIPS & TORPEDOES
	TXA
	ASL
	TAX
	LDA AY,TB1
	CMP ZX,BEAR1
	LDA AY,TB1+1
	SBC ZX,BEAR1+1
	BCC C40			;DEFINITELY NO COLLISION
	LDA ZX,BEAR1
	CLC
	ADC I,64.
	STA TEMP1
	LDA ZX,BEAR1+1
	ADC I,0
	STA TEMP1+1
	TXA
	LSR
	TAX
	LDA TEMP1
	CMP AY,TB1
	LDA TEMP1+1
	SBC AY,TB1+1
	BCS C42
C40:	LDX TEMP5
	INX
	CPX I,2
	BNE C41
	BEQ C4
C42:	LDA ZX,ACT1
	BNE C4
	LDY TEMP3
	LDA I,-1		;YES, SET TORPEDO TO INACTIVE
	STA AY,AT1
	LDA I,1
	STA ZX,ACT1		;SET SHIP TO EXPLODING
	LDA I,84
	STA LEXP
	LDA I,0
	STA ZX,INDEX1		;RESET INDEX
	LDA ZX,PICT1		;SUB-CHASER?
	CMP I,RAMMER
	BNE 1$			;NO
	LDA I,1			;YES, SET DFLAG
	STA DFLAG
	LDA I,040
	STA HEXP
	LDA I,0			;DEACTIVATE SIREN
	STA SFLG
	STA MIF
1$:	LDY ZX,PICT1
	LDA AY,PVAL		;GET SHIP POINT VALUE
	STA TEMP4
	LDA ZX,PICT1
	CMP I,RAMMER
	BEQ 5$
	LDA I,9
	JSR MESS
	LDA TEMP4
	ORA I,30
	STA A,POUT
5$:	LDA SCRL		;BUMP SCORE
	SED
	CLC
	ADC TEMP4
	STA SCRL
	LDA SCRH
	ADC I,0
	STA SCRH
	CLD
	LDA ZX,PICT1		;IF RAMMER
	CMP I,RAMMER		;IGNORE SCORE
	BEQ 4$
	LDA I,70
	STA MTIM		;ACTIVATE MESSAGE TIMER
	LDA PTACT		;IF PT BOAT INACTIVE
	BPL 4$			;THEN PUT UP POINT VALUE
	LDA I,1			;ON SCREEN ON TOPOF
	STA PTACT		;EXPLOSION
	LDY TEMP4
	LDA AY,PTSCR
	STA PTPCT
	LDA ZX,SIZE1		;ADJUST SCORE OUTPUT
	LSR			;TO POSITION SCREEN
	LSR			;DEPENDENT ON SHIPS SIZE
	LSR
	LSR
	TAY
	LDA AY,BCOR
	STA TEMP1
	TXA
	ASL
	TAY
	LDA AY,BEAR1
	CLC
	ADC TEMP1
	STA PTBEAR
	LDA AY,BEAR1+1
	ADC I,0
	AND I,3
	STA PTBEAR+1
4$:	LDX TEMP3
	RTS
PVAL:	.BYTE 1,5,1,3		;SHIP POINT VALUE TABLE
	.BYTE 0,0,0,0
	.BYTE 3,3,3,1
	.BYTE 0,0,0,0
PTSCR:  .BYTE 0F,4,0F,5,0F,6
	.SBTTL FIELD OF VIEW ROUTINES
FIELD1:	TXA
	ASL
	TAY
	LDA AY,BEAR1		;SHIPS BEARING - PERISCOPE BEARING
	SEC
	SBC BEARP
	STA TEMP4
	LDA AY,BEAR1+1
	SBC BEARP+1
	AND I,3
	STA TEMP4+1		;RESULT WITHIN 256?
	BNE FI1			;NO
	LDA TEMP4		;YES, SHIP IS VISIBLE
	STA ZX,HPOS1		;OUTPUT RESULT TO SHIP'S HPOS
	LDA I,0
	STA ZX,OFF1		;SET OFFSET=0
	BEQ FI3
FI1:	LDA BEARP		;PERISCOPE BEARING-SHIPS BEARING
	SEC
	SBC AY,BEAR1
	STA TEMP4
	LDA BEARP+1
	SBC AY,BEAR1+1
	AND I,3			;RESULT WITHIN 64?
	BNE FI2			;NO
	LDA TEMP4
	AND I,0C0
	BNE FI2			;NO
	LDA I,0			;YES
	STA ZX,HPOS1		;SHIP IS PARTLY VISIBLE
	LDA TEMP4		;SET HPOS=0, TAKE PREVIOUS
	STA ZX,OFF1		;RESULT, OUTPUT TO OFFSET
	INC ZX,VIS1		;SET VISIBILITY FLAG
FI3:	LDA I,1
	STA ZX,VIS1
	RTS
FI2:	LDA I,0
	STA ZX,VIS1
	RTS
	.SBTTL SHIP POSITION UPDATE
UPDT1:	LDA ZX,PICT1		;YES
	CMP I,RAMMER		;RAMMER?
	BEQ 14$			;YES
	INC ZX,INDEX1		;DONE?
	BEQ 11$			;YES
	LDA I,128.		;GET MIDPOINT OF SIZE CHANGE
	CMP ZX,INDEX1		;EXPANDING OR CONTRACTING?
	BCC 1$
	LDA ZX,INDEX1
	AND I,3
	BEQ 2$
	LDA ZX,SIZE1
	CLC
	ADC I,2
	STA ZX,SIZE1
	JMP 2$
1$:	LDA ZX,INDEX1
	AND I,3
	BEQ 2$
	LDA ZX,SIZE1
	SEC
	SBC I,2
	STA ZX,SIZE1
2$:	TXA
	ASL
	TAY
	LDA ZX,SIDE1		;NO, BUMP BEARING
	BEQ 12$
	LDA ZX,BEARL
	SEC
	SBC ZX,SPEED1
	STA ZX,BEARL
	LDA AY,BEAR1
	SBC I,0
	STA AY,BEAR1
	LDA AY,BEAR1+1
	SBC I,0
	AND I,3
	STA AY,BEAR1+1
	JMP 13$
12$:	LDA ZX,BEARL
	CLC
	ADC ZX,SPEED1
	STA ZX,BEARL
	LDA AY,BEAR1
	ADC I,0
	STA AY,BEAR1
	LDA AY,BEAR1+1
	ADC I,0
	AND I,3
	STA AY,BEAR1+1
13$:	RTS
11$:	LDA ZX,VIS1		;IF SHIP STILL VISIBLE, DO NOT DEACTIVATE
	BEQ 16$
	DEC ZX,INDEX1
	JMP 2$
16$:	LDA I,-1		;SHIP ALL DONE, DEACTIVATE
	STA ZX,ACT1
	LDA I,0F
	STA ZX,PICT1
	RTS
14$:	LDA ZX,SIZE1		;CAUSE RAMMER
	CLC			;TO GROW & GROW
	ADC I,1			;& GROW!
	BCC 15$
	LDA I,0FF
15$:	STA ZX,SIZE1
	JMP 2$
	.SBTTL EXPLOSION UPDATE
EXPLD:	LDA ZX,INDEX1		;1ST TIME INTO ROUTINE?
	BNE 9$			;NO
	TXA
	ASL
	TAY
	LDA AY,BEAR1
	STA AY,SBEAR
	LDA AY,BEAR1+1
	STA AY,SBEAR+1
9$:	INC ZX,INDEX1		;BUMP INDEX
	LDA ZX,INDEX1
	CMP I,40
	BCS EX1			;YES
	LDA I,1			;TURN ON EXPLOSION LITES
	STA A,ELITE
	LDA ZX,SIZE1		;CAUSE EXPLOSION
	CLC			;TO EXPAND TO MAXIMUM
	ADC I,30
5$:	STA ZX,SIZE1
	LDA ZX,SIZE1
	LSR
	LSR
	LSR
	LSR
	TAY
	LDA AY,BCOR
	STA TEMP1
	TXA
	ASL
	TAY
	LDA AY,SBEAR		;ADJUST SHIP EXPLOSION
	SEC			;BEARING TO MAKE
	SBC TEMP1		;EXPLOSION APPEAR STATIONARY
	STA AY,BEAR1
	LDA AY,SBEAR+1
	SBC I,0
	AND I,3
	STA AY,BEAR1+1
	LDA ZX,INDEX1
	CMP I,0A		;SELECT EXPLOSION PICTURE
	BCS 1$
	LDA I,0C
	BNE 4$
1$:	CMP I,14
	BCS 2$
	LDA I,0D
	BNE 4$
2$:	LDA I,0E
4$:	STA ZX,PICT1
	LDA FRAME
	AND I,2
	BNE 6$
	LDA INVRT
	EOR I,1			;FLIP VIDEO
	STA INVRT
	LDA FRAME
	AND I,2
	LSR
	STA A,ELITE
6$:	RTS
EX1:	LDA I,-1		;SET SHIP INACTIVE
	STA ZX,ACT1
	LDA I,0
	STA INVRT
	STA A,ELITE		;TURN OFF EXPLOSION LITES
	LDA I,0F
	STA ZX,PICT1
	RTS
	.SBTTL FIRE CONTROL
FIRE:	LDA A,TBUT		;FIRE BUTTON PRESSED?
	AND I,8
	BNE F1
	LDA TBUTS		;YES, PRESSED BEFORE?
	BNE F2			;YES, IGNORE IT
	LDA MODE		;IF MODE=5
	CMP I,5			;IGNORE BUTTON
	BEQ F1
	INC TBUTS		;NO, SET BUTTON STATUS
	LDA I,0
	RTS
F1:	LDA I,0			;MAKE SURE BUTTON
	STA TBUTS		;STATUS CLEAR
F2:	LDA I,-1
	RTS
	.SBTTL VOICE CONTROL
VOICE:	LDA OUTPNT
	CMP INPNT
	BEQ 2$
	LDA A,SBUSY		;SPEECH STILL BUSY?
	AND I,1
	BEQ 2$			;YES
	DEC VTOG
	BNE 2$
	LDA I,08
	STA VTOG
	LDX OUTPNT
	LDA ZX,VBUFF		;GET NEXT WORD
	STA A,SPOUT
	LDA I,1
	STA A,SPST		;START SPEECH
	LDA I,0
	STA A,SPST
	DEC OUTPNT
	DEX
	BPL 2$
	LDA I,9
	STA OUTPNT
2$:	RTS
	.SBTTL STORE SPEECH ROUTINE
STUFF:	STX TEMP5		;STORE SPEECH
	LDX INPNT		;BYTE INTO
	STA ZX,VBUFF		;CIRCULAR BUFFER
	DEC INPNT
	DEX
	BPL 1$
	LDA I,9
	STA INPNT
1$:	LDX TEMP5
	RTS
	.SBTTL SOUND ROUTINE
SOUND:	LDA SFLG		;SIREN FIRST
	BEQ 6$			;SIREN NOT ACTIVE
	LDA STOG
	BPL 1$
	CMP I,-1
	BNE 2$
	LDA I,1
	STA A,SIREN
	STA A,PROJ
	LDA I,50.
	STA STOG
	BNE 5$
2$:	INC STOG
	BNE 5$
1$:	CMP I,1
	BNE 3$
	LDA I,0
	STA A,SIREN
	LDA I,-20.
	STA STOG
	DEC SFLG
	BNE 5$
3$:	DEC STOG
	JMP 5$
6$:	LDA I,0
	STA A,SIREN
	STA A,PROJ
5$:	LDA LEXP		;LOW EXPLOSION ACTIVE?
	BEQ 7$			;NO
	DEC LEXP
	LDA LEXP
	CMP I,80
	BCC 30$
	LDA I,0
	BEQ 7$
30$:	LDA I,1
7$:	STA A,LEX
	LDA HEXP		;HI EXPLOSION ACTIVE?
	BEQ 8$			;NO
	DEC HEXP
	LDA I,1
8$:	STA A,HEX
	LDA TEXP		;TORP LAUNCH ACTIVE?
	BEQ 9$			;NO
	DEC TEXP
	LDA TEXP
	CMP I,80
	BCC 16$
	LDA I,0
	BEQ 9$
16$:	LDA I,1
9$:	STA A,TEX
	LDA SIF			;SONAR PING ACTIVE?
	BEQ 13$			;NO
	LDA SING
	BPL 11$
	CMP I,-1
	BNE 12$
	LDA I,1
	STA A,PING
	LDA I,60
	STA SING
	BNE 13$
12$:	INC SING
	BNE 13$
11$:	CMP I,1
	BNE 14$
	LDA I,0
	STA A,PING
	LDA I,-31.
	STA SING
	BNE 13$
14$:	DEC SING
13$:	LDA FRAME
	AND I,03
	BNE 26$
	LDA MIF		;MAKE MOTOR
	BNE 20$		;SOUND EITHER
	LDA VOL		;FADE IN OR
	CMP I,80	;FADE OUT
	BNE 27$
	LDA I,0
	STA A,MCON
	LDA VOL
	BNE 25$
27$:	CLC
	ADC I,10
	STA VOL
	BNE 25$
20$:	STA A,MCON
	LDA VOL
	BEQ 25$
	SEC
	SBC I,10
	STA VOL
25$:	STA A,BOTH
26$:	RTS
	.SBTTL INITILIZATION ROUTINE
INITL:	LDA I,0
	STA TOG
	STA TOGL
	STA TOGK
	STA DONE
	STA MODE
	STA DFLAG
	STA INVRT
	STA SFLG
	STA LEXP
	STA HEXP
	STA TEXP
	STA SIF
	STA MIF
	STA INDEX1
	STA INDEX1+1
	STA VIS1
	STA VIS1+1
	STA CVIS
	STA PTVIS
	STA SIZE1
	STA SIZE1
	STA HPOS1
	STA HPOS1+1
	STA OFF1
	STA OFF1+1
	STA A,LEX
	STA A,HEX
	STA A,TEX
	STA A,PING
	STA A,MCON
	STA A,COLR
	STA A,ELITE
	STA A,SIREN
	LDA I,9
	STA DTIM
	LDA I,80
	STA VOL
	LDA I,1
	STA TBUTS
	STA A,COLR
	LDA I,-1
	STA ACT1
	STA ACT1+1
	STA MTIM
	STA PTACT
	LDA I,0F
	STA PTPCT
	LDX I,0
1$:	LDA I,0
	STA ZX,HPS1
	STA ZX,VS1
	LDA I,0D0
	STA ZX,VPS1
	LDA I,-1
	STA ZX,AT1
	INX
	CPX I,2
	BNE 1$
	LDA I,0F
	STA PICT1
	STA PICT1+1
	JSR BLANK
	LDA I,0
	JSR OSCR
	LDA I,1
	JSR OSCR
	LDA I,99
	STA EVAL+1		;SET ENTENDED PLAY TO 990,000
	LDA A,TSW
	AND I,04
	BNE 2$			;IF NO EXTENDED PLAY
	LDA I,14.
	JSR MESS
	LDA A,TSW		;GET EXTENDED PLAY LEVEL
	AND I,0C0
	CLC
	ROL
	ROL
	ROL
	ROL
	TAX
	LDA AX,ETAB
	STA EVAL
	LDA AX,ETAB+1
	STA EVAL+1
	LDA EVAL
	AND I,0F0
	LSR
	LSR
	LSR
	LSR
	ORA I,30
	STA A,ELSD
	LDA EVAL+1
	AND I,0F
	BEQ 2$
	ORA I,30
	STA A,EHSD
2$:	LDA A,TSW		;SET GAME TIME
	AND I,18
	LSR
	LSR
	LSR
	TAX
	LDA AX,TTAB
	STA TUP
	STA TCNT
	LDA I,39
	STA A,TOMSD
	STA A,TOLSD
	LDA I,99
	STA TIME
	LDA A,QUIN
	AND I,3
	STA QOLD
	LDA I,0
	JSR MESS
	LDA I,1
	JSR MESS
	LDA I,6
	JSR MESS
	JSR COIN
	RTS
ETAB:	.WORD 80		;THOUSAND
	.WORD 120
	.WORD 160
	.WORD 200
	.SBTTL DESTROYER ACTIVATE
AVAT:	LDX I,0
	LDA I,0			;ACTIVATE SUB-CHASER
	STA ZX,ACT1
	STA DFLAG
	STA INVRT
	STA A,ELITE
	STA ZX,SIZE1
	STA ZX,INDEX1
	LDA POLYL
	AND I,1
	STA ZX,SIDE1
	LDA I,77
	STA ZX,SPEED1
	LDA I,1
	STA A,PTSND
	STA MIF
	LDA I,RAMMER
	STA ZX,PICT1
2$:	TXA
	ASL
	TAY
	LDA POLYL
	STA AY,BEAR1
	LDA POLYH
	AND I,3
	STA AY,BEAR1+1
	JSR FIELD1		;SEE IF SHIP VISIBLE?
	LDA ZX,VIS1		;YES, THEN OK
	BNE 1$
	JSR RANDU		;NO, TRY AGAIN
	JMP 2$
1$:	LDA OUTPNT
	STA INPNT
	LDA I,16
	JSR STUFF
	LDA I,16
	JSR STUFF
	LDA I,2
	JSR STUFF
	LDA I,13
	JSR STUFF
	RTS
	.SBTTL SCORE OUTPUT ROUTINE
OSCR:	BNE 2$
	LDA I,87
	STA TEMP1
	LDA SCRL
	STA TEMP2
	LDA SCRH
	STA TEMP2+1
	JMP 3$
2$:	LDA I,4C
	STA TEMP1
	LDA HSCR
	STA TEMP2
	LDA HSCR+1
	STA TEMP2+1
3$:	LDA I,10
	STA TEMP1+1
	LDY I,0
	STY TEMP3
	LDA TEMP2+1
	AND I,0F0
	LSR
	LSR
	LSR
	LSR
	BEQ 4$
	INC TEMP3
	ORA I,30
	STA NY,TEMP1
	INY
4$:	LDA TEMP2+1
	AND I,0F
	BNE 5$
	LDX TEMP3
	BEQ 6$
5$:	ORA I,30
	INC TEMP3
	STA NY,TEMP1
	INY
6$:	LDA TEMP2
	AND I,0F0
	LSR
	LSR
	LSR
	LSR
	BNE 7$
	LDX TEMP3
	BEQ 8$
7$:	ORA I,30
	INC TEMP3
	STA NY,TEMP1
	INY
8$:	LDA TEMP2
	AND I,0F
	ORA I,30
	STA NY,TEMP1
	INY
	LDA I,30		;OUTPUT 2 DUMMY ZEROES
	STA NY,TEMP1
	INY
	STA NY,TEMP1
	RTS
	.SBTTL QUADRATURE NMI ROUTINE
QUAD:	PHA
	TXA
	PHA
	TYA
	PHA
	CLD
	LDA A,QUIN		;IN SELF-TEST?
	AND I,10
	BEQ 11$			;YES, IGNORE PROGRAM PROTECT
	JMP 10$
11$:	LDA RTEST		;CHECK RAM CELLS
	CMP I,92
	BNE 8$
	LDA RTEST+1
	CMP I,3F
	BNE 8$
	TSX
	LDA AX,106
	CMP I,PWRON&0FF00/256.
	BCC 9$
	CMP I,PWRON+1000&0FF00/256.
	BCS 9$
	JMP 7$
8$:	LDA I,92
	STA RTEST
	LDA I,3F
	STA RTEST+1
9$:	JMP PWRON
7$:	LDA A,AMODE
	BPL 4$
	JSR OUTPT
	JSR RANDU
4$:	LDA A,QUIN		;READ QUADRATURE INPUTS
	AND I,3
	CMP QOLD		;ANY CHANGE
	BEQ 1$			;NO
	PHA			;SAVE VALUE
	LSR
	EOR QOLD		;WHICH WAY?
	LSR
	BCS 2$			;RIGHT
	LDA BEARP		;LEFT
	SEC
	SBC I,2
	STA BEARP
	LDA BEARP+1
	SBC I,0
	JMP 3$
2$:	LDA BEARP
	CLC
	ADC I,2
	STA BEARP
	LDA BEARP+1
	ADC I,0
3$:	AND I,3
	STA BEARP+1
	PLA
	STA QOLD
1$:	LDA A,COINAD		;ANY COINS?
	LDX I,0
6$:	ROL
	ROL ZX,KOIN1
	LDY ZX,KOIN1
	BNE 12$
	LDY ZX,FFLG
	BEQ 5$
	INC COINS		;YES, BUMP COINS
	LDY I,1
	STY SFLG
	LDY I,-1
	STY STOG
	LDY I,0
	STY ZX,FFLG
	BEQ 5$
12$:	CPY I,0FE
	BNE 5$
	LDY I,1
	STY ZX,FFLG
5$:	INX
	CPX I,2
	BNE 6$
	LDA A,SB		;CHECK SLAM SWITCH
	AND I,4
	BNE 10$
	LDA I,0			;IF SET, CLEAR COIN LOGIC
	STA KOIN1
	STA KOIN1+1
	STA FFLG
	STA FFLG+1
10$:	PLA
	TAY
	PLA
	TAX
	PLA
	RTI
	.SBTTL OUTPUT ROUTINE
OUTPT:	LDY I,0
	LDA A,AMODE
	AND I,10
	BNE 4$
	INY
4$:	STY CFLG
	LDA I,0
	STA A,COLR		;DO A COLLISION RESET
	LDA I,1
	STA A,COLR
	LDA INVRT
	STA A,VINT
	LDA TOG			;FLIP TOG EVERY FRAME
	STA WSHIP
	EOR I,1
	STA TOG
	BNE 1$
	LDA TOGL		;FLIP TOGL EVERY OTHER FRAME
	EOR I,1
	STA TOGL
1$:	LDX TOGL
	LDA ZX,AT1
	BMI 3$
	LDA ZX,VS1
	BEQ 3$
	LDA ZX,HPS1
	EOR I,0FF
	STA A,HPSO
	LDA ZX,VPS1
	STA A,VPSO
	LDA ZX,PCT1
	STA A,PCTO
	JMP OU2
3$:	LDA I,0
	STA A,HPSO
	LDA I,0D0
	STA A,VPSO
	LDA I,0F
	STA A,PCTO
OU2:	LDX TOG
	LDA ZX,SIZE1
	STA A,SIZEO
	LDA ZX,HPOS1
	STA A,HPOSO
	LDA I,0F
	LDY ZX,ACT1
	BMI 1$
	LDY ZX,VIS1
	BEQ 1$
	LDA ZX,PICT1
1$:	STA A,PICTO
	LDA ZX,OFF1
	STA A,OFFO
	LDA ZX,SIDE1
	EOR I,1
	STA A,REFLT
OU3:	LDA TOG
	BEQ 5$
	LDA CSEL
	ASL
	ASL
	ASL
	ASL
	ORA CPCT		;OUTPUT CLOUDS
	LDY CVIS
	BNE 6$
	LDA I,0F
6$:	STA A,PTPIK
	LDA CHPOS
	STA A,PTHOR
	LDA I,0
	STA A,HSEL
	JMP 4$
5$:	LDA SSEL
	ASL
	ASL
	ASL
	ASL
	ORA PTPCT		;OUTPUT PT BOAT
	LDY PTVIS
	BNE 2$
	LDA I,0F
2$:	STA A,PTPIK
	LDA PTHPOS
	STA A,PTHOR
	LDA I,1
	STA A,HSEL
4$:	LDA ACT1		;IF NEITHER EXPLOSION
	CMP I,1			;IS ACTIVE, MAKE
	BEQ 3$			;SURE VIDEO INVERT
	LDA ACT1+1
	CMP I,1
	BEQ 3$
	LDA PTACT		;AND EXPLOSION LITE
	CMP I,2			;ARE OFF
	BEQ 3$
	LDA MODE			;ALSO NOT IN MODE 5
	CMP I,5
	BEQ 3$
	LDA I,0
	STA A,VINT
	STA A,ELITE
3$:	RTS
TTAB:	.BYTE 38.,57.,76.,95.
BCOR:	.BYTE 0,2,3,5		;BEARING CORRECTION TABLE
	.BYTE 5,8,0A,0C		;FOR EXPLOSION SEQUENCE
	.BYTE 10,14,1A,20
	.BYTE 2A,38,48,58
SNUM:	.BYTE 8,9,9,1,2,0B,3,8
	.SBTTL COIN SUBROUTINE
COIN:	LDA COINS		;ANY COINS?
	BNE CO1			;YES
	LDA A,CSW		;NO,FREEPLAY?
	AND I,3
	BNE CO10		;NO
CO2:	LDA I,0			;YES,SET COINS=0AND
	STA COINS		;CREDIT=1
	LDA I,1
	STA CREDIT
CO10:	RTS
CO1:	LDA A,CSW		;WHAT MODE?
	AND I,3
	CMP I,3
	BNE CO3
	LDA COINS		;4 COINS 1 CREDIT
	CMP I,4			;ENOUGH COINS?
	BCC CO10		;NO
	SEC
	SBC I,4
	BPL CO7
CO3:	LDA A,CSW
	AND I,1
	BEQ CO4
	DEC COINS		;1COIN 1 CREDIT
	BPL CO5
CO4:	LDA COINS		;2 COINS 1 CREDIT
	CMP I,2			;ENOUGH COINS?
	BCC CO10		;NO
	SEC
	SBC I,2
CO7:	STA COINS
CO5:	INC CREDIT
	BPL COIN
	.SBTTL MESSAGE ROUTINE
;
;MESSAGE ROUTINE
;	ON ENTRY A-REG	=BITS 0-5 = # OF MESSAGE
;
;			=BIT 7    = 1 BLANK MESSAGE
;				    0 WRITE MESSAGE
;
MESS:	STX TEMP5
	STY TEMP5+1
	LDX I,0
	STX PTR
	STX PTR+1
	CLC
	ROL
	ROL PTR			;SAVE R/W BIT
	TAX
	LDA AX,MESG
	STA TEMP1
	LDA AX,MESG+1
	STA TEMP1+1
	LDA AX,POSM
	STA TEMP2
	LDA AX,POSM+1
	STA TEMP2+1
	LDY I,0
M1:	LDA NY,TEMP1
	BEQ M5
	LDX PTR
	BEQ M4
M2:	LDA I,0
M4:	STA NY,TEMP2
	INY
	BPL M1
M5:	LDX TEMP5
	LDY TEMP5+1
	RTS
MESG:	.WORD MES0
	.WORD MES1
	.WORD MES2
	.WORD MES3
	.WORD MES4
	.WORD MES5
	.WORD MES6
	.WORD MES7
	.WORD MES8
	.WORD MES9
	.WORD MES10
	.WORD MES11
	.WORD MES12
	.WORD MES13
	.WORD MES14
POSM:	.WORD 1098
	.WORD 1081
	.WORD 100B
	.WORD 1008
	.WORD 1008
	.WORD 1008
	.WORD 1041
	.WORD 102A
	.WORD 10AB
	.WORD 100B
	.WORD 1008
	.WORD 1086
	.WORD 1086
	.WORD 1092
	.WORD 10A2
MES0:	.ASCIZ /TIME/
MES1:	.ASCIZ /SCORE/
MES2:	.ASCIZ /FREE PLAY/
MES3:	.ASCIZ /1 COIN PER GAME/
MES4:	.ASCIZ /2 COINS PER GAME/
MES5:	.ASCIZ /4 COINS PER GAME/
MES6:	.ASCIZ /HIGH SCORE/
MES7:	.ASCIZ /EXTENDED PLAY/
MES8:	.ASCIZ /GAME OVER/
MES9:	.ASCIZ / 00 POINTS/
MES10:  .ASCIZ /PUSH START BUTTON/
MES11:	.ASCIZ /RAM OK/
MES12:	.ASCIZ /RAM BAD/
MES13:	.ASCIZ /ROM OK/
MES14:	.ASCIZ /EXTENDED PLAY FOR   000 PTS/
	.SBTTL BLANK ALPHANUMERIC RAM
BLANK:	LDX I,0
1$:	LDA I,20
	STA AX,ALPHA
	INX
	BNE 1$
	RTS
	.SBTTL WAIT SUBROUTINE
WAIT:	LDA A,AMODE
	BMI WAIT
3$:	LDA A,AMODE
	BPL 3$
	INC FRAME
	STA A,TREST		;PULSE WATCHDOG
	JSR COIN
	LDA A,QUIN
	AND I,10
	BEQ 4$
	JSR ITEST
4$:	RTS
	.SBTTL PWRON INITIALIZATION ROUTINE
SETUP:  LDA I,1
	STA A,SPST
	LDA I,1B
	STA A,SPOUT
	LDA I,0
	STA COINS
	STA CREDIT
	STA SCRL
	STA SCRH
	STA HSCR
	STA HSCR+1
	STA KOIN1
	STA KOIN2
	STA FFLG
	STA FFLG+1
	STA A,SPST
	LDA I,3A
	STA VTOG
	LDA I,9
	STA OUTPNT
	STA INPNT
	RTS
	.SBTTL INTERNAL SELF-TEST
ITEST:	JSR INITL		;TURN OFF SOUNDS
	JSR BLANK		;BLANK ALPHANUMERIC RAM
	LDX I,0FF
	TXS
	LDX I,0
1$:	TXA			;FILL UP RAM WITH TEST
	CLC			;PATTERN
	ADC I,4
	STA ZX,0
	INX
	CPX I,0F0
	BNE 1$
	LDX I,0
2$:	TXA			;NOW CHECK RAM CELLS
	CLC
	ADC I,4
	CMP ZX,0
	BNE 3$			;IF RAM FAILURE TELL USER
	INX
	CPX I,0F0
	BNE 2$
	LDA I,11.		;TELL USER RAM OK
	JSR MESS
	JMP 4$
3$:	LDA I,12.
	JSR MESS
4$:	LDA I,13.
	JSR MESS		;ROM TEST
	LDA I,96
	STA TEMP2
	LDA I,10
	STA TEMP2+1
	LDA I,PWRON&0FF00/256.
	STA TEMP4+1
	LDX I,0
5$:	LDA I,4
	STA TEMP1
	LDA I,0
	STA TEMP4
	TAY
6$:	CLC
	ADC NY,TEMP4
	INY
	STA A,TREST
	BNE 6$
	INC TEMP4+1
	DEC TEMP1
	BNE 6$
	CMP AX,CKSUM
	BEQ 7$
	TXA
	ORA I,30
	STA NY,TEMP2
	INC TEMP2
	LDA I,20
	STA NY,TEMP2
7$:	INX
	CPX I,4
	BNE 5$
	LDA I,1
	STA A,ELITE
	LDA I,0
	STA A,PROJ
	STA A,ATTK
8$:	LDA A,COINAD		;COIN
	AND I,0C0
	BNE 20$
	LDA A,SB
	AND I,20		;START BUTTON
	BNE 20$
	LDA A,TBUT
	AND I,8			;FIRE BUTTON
	BNE 11$
20$:	LDA I,1
	STA A,LEX
	LDA I,0
	TAX
	TAY
21$:	INX
	BNE 21$
	INY
	STA A,TREST
	BNE 21$
	STA A,LEX
11$:	LDA A,QUIN		;OUTPUT QUADRATURE TO SCREEN
	AND I,2
	LSR
	ORA I,30
	STA A,106F
	LDA A,QUIN
	AND I,1
	ORA I,30
	STA A,1070
	STA A,TREST
	LDA A,QUIN
	AND I,10
	BNE 8$
	JMP PWRON
CKSUM:  .BYTE 15,0AC,0C9,0,39
	.SBTTL RANDOM NUMBER GENERATOR
RANDU:	ASL POLYL
	ROL POLYH
	BPL 1$
	INC POLYL
1$:	LDA POLYL
	AND I,2
	BEQ 2$
	LDA POLYL
	EOR I,1
	STA POLYL
2$:	LDA POLYH
	ORA POLYL
	BNE 3$
	INC POLYL
3$:	RTS
	.REPT PWRON+0FFA-.
	.BYTE 0
	.ENDM
	.=PWRON+0FFA
	.WORD QUAD
	.WORD PWRON
	.WORD PWRON
	.=0F7C3
	.WORD QUAD
	.END
                                                                                                                                                                    